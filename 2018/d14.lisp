(in-package :advent/2018)

(defparameter *d14/board* "37")
(defparameter *d14/input* 323081)
(defparameter *d14/test* 9)
(defparameter *d14/size* (* 100 *d14/input*))

(defstruct scoreboard recipes pos-elf1 pos-elf2)

(defun create-board (board &key (capacity *d14/size*))
  (let ((recipes (make-array capacity
                             :element-type '(integer 0 9)
                             :adjustable t
                             :fill-pointer 0)))
    (loop :for digit :across board
       :do (vector-push (parse-integer (string digit)) recipes))
    recipes))

(defun create-scoreboard (&key (board *d14/board*))
  (make-scoreboard :recipes (create-board board) :pos-elf1 0 :pos-elf2 1))

(declaim (inline combine-recipes))

(defmethod combine-recipes (scoreboard &optional (nb-recipes 1))
  (with-slots (recipes pos-elf1 pos-elf2)
      scoreboard
    (flet ((next-position (position size)
             (mod (+ position 1 (aref recipes position)) size)))
      (loop :repeat nb-recipes
         :do (let* ((sum (+ (aref recipes pos-elf1) (aref recipes pos-elf2))))
               (multiple-value-bind (d1 d2) (truncate sum 10)
                 (unless (zerop d1) (vector-push d1 recipes))
                 (vector-push d2 recipes))

               (setf pos-elf1 (next-position pos-elf1 (length recipes)))
               (setf pos-elf2 (next-position pos-elf2 (length recipes))))))
    scoreboard))

(defmethod print-scoreboard ((scoreboard scoreboard))
  (with-slots (recipes pos-elf1 pos-elf2)
      scoreboard
    (format t "狺ア祜镳烘矧衡屐秣戾铉翳蝈汩疱螬烘矧筱矧航ㄡ蝈蝈汩疱椹恒镬戾泗ㄣ镱è痫蟓屐姹ㄦ矧磲铋洎筱矧濠è痫蟓屐娌ㄦ矧磲铋③漭筱矧濠ㄦ矧磲铋筱矧濠┅┅┅ㄤ彐礤翳镤筱矧邂镝蜾麸篝蜷铉è筱矧邂镝蜾筱矧邂镝蜾篝狎戾铉翳戾è蝈篚祠磲脲篝蜷铉戾铉翳┅祜镳烘矧烘蝻篝狎候屦遽戾铉翳烘矧烘蝻轰箦翩ㄡ蝈蝈篚祠戛ㄣ镤瀛汨狎ǐㄡ蝈筱矧邂镝蜾蝈汩疱筱矧邂镝蜾椹．ㄣ栳颦泔溴＼癌┅┅蝈篚祠┅ㄤ彐躅骈钿瞽蝈汩疱蟓徭翦颦憝蝈汩疱筱矧邂镝蜾愆筱矧邂镝蜾麸篝蜷铉ㄣ镯忾铄蝈汩疱筱矧邂镝蜾ǐ瞟瞟ㄤ彐躅翦篝骈钿瞽蝈汩疱蟓徭翦颦憝蝈汩疱īㄥ鲥蝙＇殇孱糸豉扉篝篝蜷铉戾èㄣ蝈狒瀛筱矧邂镝蜾┅ㄦ轭洵瞽蝈汩疱蟓徭翦颦憝蝈汩疱卑供⒌钡腹倍贩耿篝蜷铉戾èㄣ蝈狒瀛筱矧邂镝蜾┅ㄦ轭洵瞽蝈汩疱蟓徭翦颦憝蝈汩疱卑旦氨泊当蹈贡篝蜷铉戾èㄣ蝈狒瀛筱矧邂镝蜾┅ㄦ轭洵瞽蝈汩疱蟓徭翦颦憝蝈汩疱卑备┅⒐驳卑繁案耽篝蜷铉戾èㄣ蝈狒瀛筱矧邂镝蜾┅ㄦ轭洵瞽蝈汩疱蟓徭翦颦憝蝈汩疱卑舶备┅⒌勾贝补父并┅┅ㄤ彐躅浔疮鸨é镳糸镱犰ㄩ铕豸浔疮轭瘐舄┅戾èㄣ蝈狒瀛筱矧邂镝蜾┅ㄦ轭洵瞽蝈汩疱蟓徭翦颦憝蝈汩疱卑轭瘐舂┅｜当蹈骈蝮狃疱狎徭翦蝈汩疱螽氨泊骈蝮狃疱狎徭翦蝈汩疱螽共当骈蝮狃疱狎徭翦备蝈汩疱螽倒幢骈蝮狃疱狎徭翦舶备蝈汩疱螽ㄤ彐躅骈钿箦聃孱沐筱矧邂镝蜾翎蜱弭镳糸镱犰钼蝈汩疱暴戾舄è箦聃孱沐ㄣ蝈狒瀛怙狎翎蜱弭┅祜镳烘矧篝狎航呼桢ǐ篝狎钼蝈汩疱螬轰ㄣ镯忾铄蝈汩疱筱矧邂镝蜾钼蝈汩疱螬呼桢蝈轶箦狎汨箦聃孱沐筱矧邂镝蜾蝈汩疱筱矧邂镝蜾后翎螋篝狎舂┅ㄤ彐躅翦篝骈钿箦聃孱沐īㄥ鲥蝙＇殇孱糸豉扉篝ㄦ轭洵箦聃孱沐ㄣ蝈狒瀛筱矧邂镝蜾⒌钡腹卑┅ㄦ轭洵箦聃孱沐ㄣ蝈狒瀛筱矧邂镝蜾氨泊耽卑┅备ㄦ轭洵箦聃孱沐ㄣ蝈狒瀛筱矧邂镝蜾⒐驳卑舶┅舶备ㄦ轭洵箦聃孱沐ㄣ蝈狒瀛筱矧邂镝蜾⒌勾贝嘲鞍┅┅ㄤ彐躅浔疮鸩é镳糸镱犰ㄩ铕豸浔疮轭瘐舄┅戾è翎蜱弭痱轭悱麸篝蜷铉轭瘐舂┅ㄦ轭洵箦聃孱沐ㄣ蝈狒瀛筱矧邂镝蜾翎蜱弭浔疮箝濯┅ㄤ彐躅浔疮篚眄狎īㄦ矧磲⒛狴贝描镢镬狒描狎趔アㄦ矧磲絮戾焙筱矧弩镦翳翦蝈汩疱轫礤溟狒屐徭翦巢嘲副ア痱轭舡蝈篚祠ㄤ贝鸨┅ㄦ矧磲絮戾埠栾磲铢蝈汩疱麸翳戾骠镦巢嘲副ア痱轭舡蝈篚祠ㄤ贝鸩┅